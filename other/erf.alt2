c
c
c     ###################################################
c     ##  COPYRIGHT (C)  2008  by  Jay William Ponder  ##
c     ##              All Rights Reserved              ##
c     ###################################################
c
c     ###############################################################
c     ##                                                           ##
c     ##  function erf  --  evaluate error function via Chebyshev  ##
c     ##                                                           ##
c     ###############################################################
c
c
c     "erf" evaluates the error function via Chebyshev approximation
c
c
      function erf (x)
      implicit none
      real*8 x,z
      real*8 erf,erfc
      external erfc
c
c
c     get error function value via difference from the complement
c
      z = 1.0d0 - erfc(x)
      erf = z
      return
      end
c
c
c     ################################################################
c     ##                                                            ##
c     ##  function erfc  --  evaluate complementary error function  ##
c     ##                                                            ##
c     ################################################################
c
c
c     "erfc" evaluates the complementary error function via a
c     Chebyshev approximation
c
c     literature references:
c
c     W. H. Press, S. A. Teukolsky, W. T. Vetterling and B. P.
c     Flannery, Numerical Recipes (Fortran), 2nd Ed., Cambridge
c     University Press, 1992, Section 6.2
c
c
      function erfc (x)
      implicit none
      real*8 x,y,z
      real*8 erfc
c
c
c     get Chebyshev approximation to complementary error function
c
      y = 1.0d0 / (1.0d0+0.5d0*abs(x))
      z = y * exp(-x*x - 1.26551223d0 + y*(1.00002368d0
     &               + y*(0.37409196d0 + y*(0.09678418d0
     &               + y*(-0.18628806d0 + y*(0.27886807d0
     &               + y*(-1.13520398d0 + y*(1.48851587d0
     &               + y*(-0.82215223d0 + y*0.17087277d0)))))))))
      if (x .lt. 0.0d0)  z = 2.0d0 - z
      erfc = z
      return
      end
